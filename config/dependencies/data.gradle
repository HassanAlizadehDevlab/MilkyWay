/**
 * Declare all dependencies for the data layer
 * */
apply plugin: 'com.apollographql.apollo'

def apiProperties = new Properties()
apiProperties.load(new FileInputStream(rootProject.file('api.properties')))

android {
    defaultConfig {
        buildConfigField "String", "BASE_URL_GRAPHQL", "\"${apiProperties['BASE_URL_GRAPHQL']}\""
        buildConfigField "String", "BASE_URL_REST", "\"${apiProperties['BASE_URL_REST']}\""
        buildConfigField "String", "KEY", "\"${apiProperties['KEY']}\""
    }

    sourceSets {
        test {
            java {
                srcDir "../common_test/src/test/java"
            }
            resources {
                srcDir "../common_test/src/test/resources"
            }
        }
    }
}

dependencies {
    def libraries = rootProject.ext.libraries

    implementation project(':domain')
    implementation project(':common')

    kapt libraries.room.compiler
    implementation libraries.gson
    implementation libraries.retrofit.retrofit
    implementation libraries.retrofit.converterGson
    implementation libraries.retrofit.rxjavaAdapter
    implementation libraries.okHttp.logging
    implementation libraries.room.runtime
    implementation libraries.room.rxJava
    implementation libraries.apollo.android
    implementation libraries.apollo.rxJava

    testImplementation project(':common_test')
}
